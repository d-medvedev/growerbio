on System#Boot do
Event,cfgCdTsk
Event,cfgLtTsk
Event,cfgWtrTsk
TaskValueSet CdSgs,vlvSts,0
TaskValueSet,swVer,Maj,3
TaskValueSet,swVer,Min,1
TaskValueSet,swVer,Fix,0
Endon
On Clock#Time=All,**:** Do
If [AutoCtrlSgs#cd] = 1 Or [AutoCtrlSgs#lt] = 1
 TaskValueSet,TmEvtCntr,cd,[TmEvtCntr#cd]+1
 TaskValueSet,TmEvtCntr,lt,[TmEvtCntr#lt]+1
 Let,1,3600*([LtSgs#dStrtHr])+60*[LtSgs#dStrtMn]
 Let,2,3600*([LtSgs#dStrtHr]+[LtSgs#dDrtn])+60*[LtSgs#dStrtMn]
 Let,3,%unixday_sec%+3600*[LtSgs#tZn]
 If %v3% > 24*3600
  Let,3,[VAR#3]-24*3600
 Endif
 Let,4,[TmEvtCntr#cd]%[CdSgs#Gap]
 If %v2% > 24*3600
  Let,2,[VAR#2]-24*3600
  If %v3% >= %v1% Or %v3% <= %v2%
   TaskValueSet, cycleCtrl,ltCond,1
  Else
   TaskValueSet, cycleCtrl,ltCond,0
  Endif
 Else
  If %v3% >= %v1% And %v3% <= %v2%
   TaskValueSet, cycleCtrl,ltCond,1
  Else
   TaskValueSet, cycleCtrl,ltCond,0
  Endif
 Endif
 If [cycleCtrl#ltCond] = 1
  If [AutoCtrlSgs#cd] = 1 And %v4% = 0
   Event, cdTmrEvt
  Endif
  If [AutoCtrlSgs#lt] = 1
   Event, ltTmrEvt
  Endif
 Else
  GPIO [rlPins#cd],0
  GPIO [rlPins#lt],0
 Endif
Endif
If [AutoCtrlSgs#wtr] = 1 
 TaskValueSet WtrSgs, wtrIntvl,([LtSgs#dDrtn]-2*[WtrSgs#wtrShft])*60/([WtrSgs#wtrCntr]-1)
 Let,5,3600*([LtSgs#dStrtHr]+[WtrSgs#wtrShft])+60*[LtSgs#dStrtMn]
 Let,6,3600*([LtSgs#dStrtHr]+[LtSgs#dDrtn])+60*[LtSgs#dStrtMn]
 Let,7, %unixday_sec%+3600*[LtSgs#tZn]
 If %v7% > 24*3600
  Let,7,[VAR#7]-24*3600
 Endif
 Let,8, [TmEvtCntr#wtr]%[WtrSgs#wtrIntvl]
 
 If %v6% > 24*3600
 Let,6,[VAR#6]-24*3600
  If %v7% >= %v5% Or %v7% <= %v6%+3600
   TaskValueSet, cycleCtrl,wtrCond,1
  Else
   TaskValueSet, cycleCtrl,wtrCond,0
  Endif
 Else
  If %v7% >= %v5% And %v7% <= %v6%+3600
   TaskValueSet, cycleCtrl,wtrCond,1
  Else
   TaskValueSet, cycleCtrl,wtrCond,0
  Endif
 Endif
 
 If [cycleCtrl#wtrCond] = 1
   TaskValueSet,Wtr,Ena,1
 Else
  TaskValueSet,Wtr,Ena,0
  TaskValueSet Wtr,Cntr, 0
  TaskValueSet,TmEvtCntr,wtr,0
 Endif 
 If [Wtr#Ena] = 1
  If [Wtr#Cntr] < [WtrSgs#wtrCntr]
   If %v8% = 0
    Event,wtrTmrEvt  
   Endif
  Else
   TaskValueSet,Wtr,Ena,0
   TaskValueSet Wtr,Cntr, 0   
  Endif  
  TaskValueSet,TmEvtCntr,wtr,[TmEvtCntr#wtr]+1
 Endif
If [Scd40#cd] = 0
 Reboot
Endif
Publish %sysname%/status/LWT,1
Endif
Endon

On MqAutoCtrl#glblAuto Do
 Event, cdTmrEvt
 Event, ltTmrEvt
 Event, wtrTmrEvt
Endon

On MqStsReq#all Do
 Event, updDlSgs
 Event, updCdSgs
 Event, updWtrSgs
 Publish %sysname%/status/sw_ver,[swVer#Maj].[swVer#Min].[swVer#Fix]
 Publish %sysname%/status/ip_addr,%ip%
 Publish %sysname%/status/LWT,1
Endon

On MqStsReq#reboot Do
  Reboot
Endon